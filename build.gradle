plugins {
    id("java")
}

group = "us.q3q"
version = "1.0-SNAPSHOT"

repositories {
    mavenCentral()
}

configurations {
    jcdk
}

dependencies {
    // JCDK dependencies for CAP generation
    jcdk files("${System.getenv('JC_HOME')}/lib/api_classic-${JavaCardVersion}.jar")
    
    // Test dependencies use JCardSim
    implementation 'com.klinec:jcardsim:3.0.6.0'
    testImplementation 'org.junit.jupiter:junit-jupiter:5.11.4'
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
}

test {
    useJUnitPlatform()
}

tasks.register('testJar', Jar) {
    archiveBaseName = project.name + '-tests'
    duplicatesStrategy = 'include'
    from sourceSets.test.output + sourceSets.test.allSource
    from {
        sourceSets.test.runtimeClasspath.filter {
            it.toString().indexOf("jcardsim-") != -1
        }.collect {
            zipTree(it)
        }
    }
}

// Special task for compiling with JCDK API
task classesCap(type: JavaCompile) {
    source = sourceSets.main.java
    classpath = configurations.jcdk
    destinationDirectory = file("${buildDir}/classes/jcdk")
    sourceCompatibility = 1.7
    targetCompatibility = 1.7
}

task(buildCap, type: JavaExec) {
    mainClass = "com.sun.javacard.converter.Main"
    classpath = files("${System.getenv('JC_HOME')}/lib/tools.jar")
    args "-classdir", "${buildDir}/classes/jcdk",
         "-applet", "${AppletAID}",
         "${PackageName}.FIDO2Applet",
         "-target", "${JavaCardVersion}",
         "${PackageName}",
         "${PackageAID}",
         "${PackageVersion}"
}

buildCap.dependsOn classesCap
build.dependsOn buildCap
